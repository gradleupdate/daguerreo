/*
 * Copyright 2015 Yusuke Ikeda
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext {
        springBootVersion = '1.3.0.M2'
        h2Version = '1.4.+'
        flywayVersion = '3.2.1'
        jooqVersion = '3.6.2'
        lombokVersion = '1.16.4'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.h2database:h2:${h2Version}"
        classpath "org.flywaydb:flyway-gradle-plugin:${flywayVersion}"
        classpath "org.jooq:jooq-codegen:${jooqVersion}"
    }
}

allprojects {
    group = 'org.yukung'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext {
        javaVersion = JavaVersion.VERSION_1_8
    }
}

subprojects {
    apply plugin: 'groovy'  // Groovy plugin that contains Java plugin, It's not necessary to specify the Java Plugin.

    targetCompatibility = sourceCompatibility = javaVersion
    tasks.withType(AbstractCompile)*.options*.encoding = 'UTF-8'

    configurations {
        provided
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
            downloadJavadoc = true
            downloadSources = true
        }
    }

    eclipse {
        classpath {
            containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
            containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
            plusConfigurations += [configurations.provided]
            downloadJavadoc = true
            downloadSources = true
        }
    }

    /*
     * Behavior of Gradle Application Plugin has been changed from 2.3,
     * will also change the behavior of the Spring Boot Plugin by its influence,
     * mainClass the distribution of tasks are executed even if you disable the bootRepackage task
     * because it has not been specified build fails.
     *
     * This you want to disable the distribution of tasks as a work-around.
     * This issue is expected to be fix in Spring Boot 1.3.0.M3.
     *
     * See this issue for more information: https://github.com/spring-projects/spring-boot/issues/2679
     */
    afterEvaluate { subproject ->
        if (subproject.name != 'daguerreo-backend-api') {
            subproject.tasks.matching {
                it.name in ['findMainClass', 'startScripts', 'distTar', 'distZip']
            }.each { task ->
                task.enabled = false
            }
        }
    }
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
        vcs = 'Git'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

project('daguerreo-initdb') {
    description = 'Daguerreo initialize database'

    jar.enabled = false

    apply plugin: 'org.flywaydb.flyway'

    flyway {
        url = 'jdbc:h2:file:/tmp/daguerreo'
        user = 'sa'
    }

    flywayMigrate.mustRunAfter build
}

project('daguerreo-persistence') {
    description = 'Daguerreo persistence layer'

    apply plugin: 'spring-boot'

    bootRepackage.enabled = false

    dependencies {
        compile "org.springframework.boot:spring-boot-starter-jooq"
        compile "org.springframework.boot:spring-boot-starter-validation"
        runtime "com.h2database:h2"
//        runtime "mysql:mysql-connector-java"
        testCompile "org.springframework.boot:spring-boot-starter-test"
        testCompile "org.codehaus.groovy:groovy-all:2.4.1"
        testCompile "org.spockframework:spock-spring:1.0-groovy-2.4"
    }

    task jooqGen(dependsOn: ':daguerreo-initdb:flywayMigrate') {
        description = 'Using the source code generation function of jOOQ, it will generate dao, entity, and more from the DB schema.'

        inputs.dir file("${project(':daguerreo-initdb').buildDir}/resources/main/db/migration")
        outputs.dir file("$projectDir/src/main/java/org/yukung/daguerreo/infrastructure/generated")
        doLast {
            def writer = new StringWriter()
            def xml = new groovy.xml.MarkupBuilder(writer)
            // Workaround to avoid the problem of namespace are duplicated for the property of Gradle.
            // See: http://stackoverflow.com/questions/7437355/how-to-avoid-outer-scope-values-messing-with-using-the-markupbuilder
            xml.configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.6.0.xsd') {
                jdbc() {
                    driver 'org.h2.Driver'
                    url 'jdbc:h2:file:/tmp/daguerreo'
                    user 'sa'
                    password ''
                }
                generator() {
                    database() {
                        xml.name 'org.jooq.util.h2.H2Database'
                        includes '.*'
                        excludes 'schema_version'
                        inputSchema 'PUBLIC'
                        customTypes() {
                            ['LocalDate', 'LocalTime', 'LocalDateTime'].each { value ->
                                customType() {
                                    xml.name value
                                    type "java.time.$value"
                                    converter "org.yukung.daguerreo.infrastructure.util.${value}Converter"
                                }
                            }
                        }
                        forcedTypes() {
                            ['LocalDate'    : [/.*\..*_on$/, 'DATE'],
                             'LocalTime'    : [/.*\..*_at$/, 'TIME'],
                             'LocalDateTime': [/.*\..*_at$/, 'TIMESTAMP']
                            ].each { key, value ->
                                forcedType() {
                                    xml.name key
                                    expression value[0]
                                    types value[1]
                                }
                            }
                        }
                    }
                    strategy() {
                        xml.name 'org.yukung.daguerreo.infrastructure.generator.SingularNamingGeneratorStrategy'
                    }
                    generate() {
                        daos true
                        validationAnnotations true
                    }
                    target() {
                        packageName 'org.yukung.daguerreo.infrastructure.generated'
                        directory "$projectDir/src/main/java"
                    }
                }
            }
            org.jooq.util.GenerationTool.generate(
                    javax.xml.bind.JAXB.unmarshal(
                            new StringReader(writer.toString()),
                            org.jooq.util.jaxb.Configuration.class
                    )
            )
        }
    }

    compileJava.dependsOn jooqGen
}

project('daguerreo-domain') {
    description = 'Daguerreo domain layer'

    apply plugin: 'spring-boot'

    bootRepackage.enabled = false

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }

    dependencies {
        compile project(':daguerreo-persistence')
        provided "org.projectlombok:lombok:${lombokVersion}"
        testCompile "org.springframework.boot:spring-boot-starter-test"
        testCompile "org.codehaus.groovy:groovy-all:2.4.1"
        testCompile "org.spockframework:spock-spring:1.0-groovy-2.4"
    }

    compileJava.dependsOn ':daguerreo-persistence:build'
}

project('daguerreo-backend-api') {
    description = 'Daguerreo backend REST API'

    apply plugin: 'spring-boot'

    jar {
        baseName = project.name
        version = project.version
    }

    dependencies {
        compile project(':daguerreo-domain')
        compile "org.springframework.boot:spring-boot-starter-actuator"
        compile "org.springframework.boot:spring-boot-devtools"
        compile "org.springframework.boot:spring-boot-starter-security"
        compile "org.springframework.boot:spring-boot-starter-thymeleaf"
        compile "org.springframework.boot:spring-boot-starter-web"
        testCompile "org.springframework.boot:spring-boot-starter-test"
        testCompile "org.codehaus.groovy:groovy-all:2.4.1"
        testCompile "org.spockframework:spock-spring:1.0-groovy-2.4"
    }

    compileJava.dependsOn ':daguerreo-domain:build'
}
